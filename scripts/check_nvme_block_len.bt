#!/usr/bin/bpftrace
// SPDX-License-Identifier: GPL-2.0-only
/*
 * Observability of block size and LBA aligment for the NVMe command sizes for
 * the given drive and requested operation.
 *
 * Notes:
 * - Use of associative arrays in h_aligned and h_naligned to workaround issue
 * when clearing, bpftrace will print anyway the variable.
 *
 * Inputs (kprobe:nvme_setup_cmd):
 * $1: Disk name (e.g. nvme0n1).
 * $2: Request operation [^req_op] (e.g. 1).
 * $3: Block size (power of 2) to monitor.
 * $4: Enable debug traces.
 *
 * [^req_op]: Example 1 == "REQ_OP_WRITE". More in req_op enum at
 * 'include/linux/blk_types.h' file).
 *
 * Copyright (c) 2023 Samsung Electronics Co., Ltd. All Rights Reserved.
 *
 * Author:
 * Daniel Gomez <da.gomez@samsung.com>
 */
#ifndef BPFTRACE_HAVE_BTF
#include <linux/blk-mq.h>
#include "drivers/nvme/host/nvme.h"
#endif /* BPFTRACE_HAVE_BTF */

BEGIN
{
	@filt_disk = str($1);
	@rop = $2;
	@alig_size = (uint32)$3;
	@debug = $4;
	printf("Tracing NVMe command size... Hit Ctrl-C to end.\n");
	if (@debug) {
		printf("Debug mode enabled\n");
		printf("Aligment: %u\n", $3);
		printf("%-9s %-6s %-6s %-8s %-6s %-16s\n", "DISK", "ALIGN", "LBA", "LEN", "OP", "COMM");
	}
}

kprobe:nvme_setup_cmd
{

	$req = (struct request *)arg1;
	$disk = $req->q->disk->disk_name;
	$lba_len = (uint32)($3/4096);
	$sector_shift = 9;

	if ($disk != str($1)) {
		return;
	}

	$req_op = $req->cmd_flags & 0xff;
	if ($req_op != $2) {
		return;
	}

	$ns = (struct nvme_ns *)arg0;
	$lba_shift = $ns->lba_shift;
	$lba = $req->__sector >> ($lba_shift - $sector_shift);

	$block_len = $req->__data_len;
	$aligned = (!($block_len % @alig_size)) && (!($lba % $lba_len));

	if ($aligned) {
		@h_aligned[@alig_size] = count();
		@c_data_aligned[$block_len] = count();
	} else {
		@h_naligned[@alig_size] = count();
		@c_data_naligned[$block_len] = count();
	}

	if (@debug) {
		printf("%-9s %-6u %-6u %-8u %-6u %-16s\n", $disk, $aligned, $lba, $block_len, $req_op, comm);
	}

	@comm[comm] = count();
	@h_data_len = hist($block_len);
}

END
{
	printf("\n\n--------------------------------------------------------------------------------");
	printf("\nTracing completed:\n");
	printf("* Disk: %21s\n* Aligned block size: %u\n* Request operation: %2u", @filt_disk, @alig_size, @rop);

	printf("\n\nAligned operations:\n");
	print(@h_aligned);
	print(@h_naligned);

	printf("\nBlock size histogram:\n");
	print(@h_data_len);

	printf("\nBlock sizes list:\n");
	print(@c_data_aligned);
	print(@c_data_naligned);

	printf("\nList of processes:\n");
	print(@comm);
	printf("--------------------------------------------------------------------------------");

	clear(@c_data_aligned);
	clear(@c_data_naligned);
	clear(@comm);
	delete(@h_data_len);
	delete(@rop);
	delete(@filt_disk);
	delete(@alig_size);
	clear(@h_aligned);
	clear(@h_naligned);
	clear(@h_data_len);
	delete(@h_data_len);
	delete(@debug);
}
